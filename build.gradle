// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext {
        kotlin_version = '1.7.0'
        compose_version = '1.2.0'
    }

    dependencies {
        classpath "com.android.tools.build:gradle:7.2.0"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'com.android.application' version '7.2.0' apply false
    id 'com.android.library' version '7.2.0' apply false
}

apply from: "$rootDir/config.gradle"

allprojects {
    afterEvaluate {
        apply from: "$rootDir/deps.gradle"

        if (it.hasProperty('android')) {
            android {
                compileSdk rootProject.config.compileSdk

                defaultConfig {
                    minSdk rootProject.config.minSdk
                    targetSdk rootProject.config.targetSdk
                    versionCode rootProject.config.versionCode
                    versionName rootProject.config.versionName

                    vectorDrawables {
                        useSupportLibrary true
                    }
                }
                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_8
                    targetCompatibility JavaVersion.VERSION_1_8
                }
                kotlinOptions {
                    jvmTarget = '1.8'
                }
                buildFeatures {
                    viewBinding true
                    compose true
                }

                composeOptions {
                    kotlinCompilerExtensionVersion compose_version
                }

                packagingOptions {
                    resources {
                        excludes += '/META-INF/{AL2.0,LGPL2.1}'
                    }
                }

                if (getPluginManager().hasPlugin('com.google.devtools.ksp')) {
                    if (getPluginManager().hasPlugin('com.android.application')) {
                        applicationVariants.all { variant ->
                            kotlin.sourceSets {
                                getByName(variant.name) {
                                    file("$projectDir/build/generated/ksp/${variant.name}/kotlin")
                                    kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
                                }
                            }
                        }
                    }

                    if (getPluginManager().hasPlugin('com.android.library')) {
                        libraryVariants.all { variant ->
                            kotlin.sourceSets {
                                getByName(variant.name) {
                                    kotlin.srcDir("build/generated/ksp/${variant.name}/kotlin")
                                }
                            }
                        }
                    }
                }
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}